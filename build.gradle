buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = "${gunsrpg_version}"
group = 'dev.toma.gunsrpg'
archivesBaseName = "gunsrpg-${mc_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'official', version: '1.16.5'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"

            // Workaround for new Mojang Authlib, See https://github.com/FabricMC/fabric-loom/issues/915#issuecomment-1609154390
            property 'minecraft.api.auth.host', 'https://nope.invalid'
            property 'minecraft.api.account.host', 'https://nope.invalid'
            property 'minecraft.api.session.host', 'https://nope.invalid'
            property 'minecraft.api.services.host', 'https://nope.invalid'

            arg "-mixin.config=gunsrpg.mixins.json"

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"

            // Workaround for new Mojang Authlib, See https://github.com/FabricMC/fabric-loom/issues/915#issuecomment-1609154390
            property 'minecraft.api.auth.host', 'https://nope.invalid'
            property 'minecraft.api.account.host', 'https://nope.invalid'
            property 'minecraft.api.session.host', 'https://nope.invalid'
            property 'minecraft.api.services.host', 'https://nope.invalid'

            arg "-mixin.config=gunsrpg.mixins.json"
            arg "nogui"

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url "https://www.cursemaven.com" }
    maven { url "https://dvs1.progwml6.com/files/maven/" }
    maven { url "https://modmaven.k-4u.nl" }
    flatDir { dir 'libs' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    implementation fg.deobf("curse.maven:configuration-${configuration_projectId}:${configuration_fileId}")
    compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")
    testImplementation('org.junit.jupiter:junit-jupiter:5.9.0')
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title": "gunsrpg",
                "Specification-Vendor": "Toma",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"Toma",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "gunsrpg.mixins.json"
        ])
    }
}

test {
    useJUnitPlatform()
}

mixin {
    add sourceSets.main, "gunsrpg.refmap.json"
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
